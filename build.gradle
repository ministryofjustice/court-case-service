import java.time.Instant
import java.time.LocalDate

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.2'
    id 'io.spring.dependency-management' version '1.1.3'
    id 'com.github.ben-manes.versions' version '0.42.0'
    id "org.flywaydb.flyway" version "9.22.1"
    id 'jacoco'
    id "au.com.dius.pact" version "4.3.15"
    id 'java-library'
    id 'org.jetbrains.kotlin.jvm' version '1.9.20-Beta2'
    id "org.jetbrains.kotlin.plugin.spring" version "1.9.20-Beta2"
    id 'org.jetbrains.kotlin.plugin.lombok' version '1.9.20-Beta2'
}


repositories {
    mavenCentral()
    maven {
        url 'https://s3-us-west-2.amazonaws.com/dynamodb-local/release'
    }
    flatDir {
        dirs 'libs'
    }
}

group = 'uk.gov.justice.probation'

ext {
    restAssuredVersion = '5.3.0'
    pactVersion = '4.3.15'
}

def today = Instant.now()
def todaysDate = LocalDate.now().format('yyyy-MM-dd')
version = System.getenv('CI') ? "${todaysDate}.${System.getenv('CIRCLE_BUILD_NUM')}" : todaysDate

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    agentDeps.transitive = false
}

jar {
    enabled = false
    manifest {
        attributes(
                'Implementation-Title': rootProject.name, 'Implementation-Version': archiveVersion
        )
    }
}

bootJar {
    manifest = jar.manifest
}

flyway {
    url = 'jdbc:postgresql://localhost:5432/postgres?currentSchema=courtcaseservice&user=root&password=dev'
}

springBoot {
    buildInfo()
}

test {
    useJUnitPlatform()
    filter {
        excludeTestsMatching '*VerificationPactTest'
        excludeTestsMatching '*SmokeTest'
        excludeTestsMatching '*WIP*'
    }

    testLogging {
        events "started", "passed", "skipped", "failed", "standardError"
        exceptionFormat "short"
        showStackTraces = true
        showExceptions = true
        showCauses = true

        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            }
        }
    }
}

task integrationTest(type: Test) {
    useJUnitPlatform()
    filter {
        includeTestsMatching '*IntTest*'
    }
}

task pactVerifyPublish(type: Test) {
    useJUnitPlatform()
    filter {
        excludeTestsMatching '*IntTest'
        includeTestsMatching '*VerificationPactTest'
    }

    systemProperty("pact.provider.tag", System.getenv("PACT_PROVIDER_TAG"))
    systemProperty("pact.provider.version", System.getenv("PACT_PROVIDER_VERSION"))
    systemProperty("pact.verifier.publishResults", true)
    systemProperty("pactbroker.host", System.getenv("PACTBROKER_HOST"))

    testLogging {
        events "started", "passed", "skipped", "failed", "standardError"
        exceptionFormat "short"
        showStackTraces = true
        showExceptions = true
        showCauses = true

        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            }
        }
    }
}

task pactTestPublish dependsOn pactVerifyPublish

task unitTest(type: Test) {
    useJUnitPlatform()
    filter {
        excludeTestsMatching '*IntTest'
        excludeTestsMatching '*SmokeTest'
        excludeTestsMatching '*WIP*'
        excludeTestsMatching '*VerificationPactTest'
    }
}

task wipTest(type: Test) {
    filter {
        includeTestsMatching '*WIP*'
    }
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }
}

kotlin {
    jvmToolchain(21)
}

tasks.withType(JavaCompile).configureEach {
    javaCompiler = javaToolchains.compilerFor {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

dependencies {
    modules {
        module("org.codehaus.groovy:groovy") {
            replacedBy("org.apache.groovy:groovy", "conflicts in current rest-assured version")
        }
    }

    implementation("net.minidev:json-smart:2.4.9") {
        because("SNYK scan reported secuity vulnerability in the bundled net.minidev:json-smart:2.4.8")
    }

    // Open API Documentation
    implementation group: 'org.springdoc', name: 'springdoc-openapi-starter-webmvc-ui', version: '2.5.0'

    // SAR (Subject Access Request) endpoint is included here
    implementation("uk.gov.justice.service.hmpps:hmpps-kotlin-spring-boot-starter:0.2.1") {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-oauth2-resource-server'
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-security'
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-oauth2-client'
    }

    implementation("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.15.3")
    implementation('com.fasterxml.jackson.module:jackson-module-kotlin:2.17.2')

    annotationProcessor 'org.projectlombok:lombok:1.18.32'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    api 'org.projectlombok:lombok:1.18.32'

    runtimeOnly 'org.flywaydb:flyway-core'
    runtimeOnly "org.springframework.boot:spring-boot-devtools"
    runtimeOnly 'org.postgresql:postgresql:42.6.1'

    implementation 'org.projectreactor:reactor-spring:1.0.1.RELEASE'

    implementation ('org.springframework.boot:spring-boot-starter-webflux')
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation("org.springframework.retry:spring-retry:1.3.4")
    implementation('org.springframework.security:spring-security-oauth2-client')
    implementation 'org.springframework.security:spring-security-oauth2-resource-server'
    implementation 'org.springframework.security:spring-security-oauth2-jose'
    implementation('org.springframework.data:spring-data-envers')

    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-reactor")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-jdk9")

    implementation("com.fasterxml.jackson.module:jackson-module-kotlin:2.17.2")


    implementation 'io.jsonwebtoken:jjwt:0.12.5'
    implementation 'com.microsoft.azure:applicationinsights-spring-boot-starter:2.6.4'
    implementation 'com.microsoft.azure:applicationinsights-logging-logback:2.6.4'
    implementation 'net.logstash.logback:logstash-logback-encoder:7.2'

    implementation("uk.gov.justice.service.hmpps:hmpps-sqs-spring-boot-starter:2.1.1")

    implementation group: 'ai.h2o', name: 'h2o-genmodel', version: '3.38.0.3'

    implementation 'commons-io:commons-io:2.15.1'

    testAnnotationProcessor 'org.projectlombok:lombok:1.18.32'

    testImplementation "org.testcontainers:postgresql:1.17.6"
    testImplementation "org.testcontainers:junit-jupiter:1.20.1"
    testImplementation "org.testcontainers:localstack:1.20.1"
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'net.javacrumbs.json-unit:json-unit-assertj:2.35.0'
    testImplementation("io.rest-assured:json-path:$restAssuredVersion")
    testImplementation("io.rest-assured:xml-path:$restAssuredVersion")
    testImplementation("io.rest-assured:rest-assured:$restAssuredVersion")
    testImplementation("io.rest-assured:rest-assured-common:$restAssuredVersion")

    testImplementation(platform("org.junit:junit-bom:5.9.1"))
    testImplementation("org.junit.jupiter:junit-jupiter")

    testImplementation("org.wiremock:wiremock:3.4.2")

    testImplementation("au.com.dius.pact.provider:junit5spring:$pactVersion")
    testImplementation("au.com.dius.pact.provider:junit5:$pactVersion")
    testImplementation("au.com.dius.pact:consumer:$pactVersion")
    testImplementation("au.com.dius.pact.consumer:junit5:$pactVersion")

    testImplementation("org.apache.httpcomponents:fluent-hc:4.5.13")
    testImplementation("org.awaitility:awaitility-kotlin:4.2.0")
    testImplementation("org.mockito.kotlin:mockito-kotlin:5.4.0")

    agentDeps 'com.microsoft.azure:applicationinsights-agent:3.5.2'
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.9.10")

    // Spring ignores constructor binding on Kotlin data classes without this which in turn breaks hmpps-sqs-spring-boot-starter autoconfiguration
    runtimeOnly 'org.jetbrains.kotlin:kotlin-reflect:1.7.10'
}

jacoco {
    toolVersion = '0.8.11'
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            enabled = true
            element = 'CLASS'
            includes = ['uk.gov.justice.probation.*']
            excludes = ['uk.gov.justice.probation.courtcaseservice.jpa.repository.*']

            limit {
                counter = 'LINE'
                value = 'TOTALCOUNT'
                minimum = 0.8
            }
            limit {
                counter = 'METHOD'
                value = 'TOTALCOUNT'
                minimum = 0.8
            }
        }
    }
}

task copyAgent(type: Copy) {
    from configurations.agentDeps
    into "$buildDir/libs"
}

task copyAgentConfig(type: Copy) {
    from "applicationinsights.json"
    into "$buildDir/libs"
}

task installGitHooks(type: Copy) {
    from new File(rootProject.rootDir, 'hooks/')
    into { new File(rootProject.rootDir, '.git/hooks') }
}


compileKotlin {
    kotlinOptions.jvmTarget = "21"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "21"
}

pact {
    publish {
        pactBrokerUrl = System.getenv("PACTBROKER_URL")
        pactBrokerUsername = System.getenv("PACTBROKER_AUTH_USERNAME")
        pactBrokerPassword = System.getenv("PACTBROKER_AUTH_PASSWORD")
        pactDirectory = 'build/pacts'
        consumerVersion = System.getenv("PACTCONSUMER_VERSION")
    }
}

assemble.dependsOn copyAgent
assemble.dependsOn copyAgentConfig



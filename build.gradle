import java.time.Instant
import java.time.LocalDate

plugins {
    id 'java'
    id 'org.springframework.boot' version '2.4.4'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'com.github.ben-manes.versions' version '0.38.0'
    id "org.flywaydb.flyway" version "7.7.3"
    id 'jacoco'
}

repositories {
    jcenter()
    mavenCentral()
    maven {
        url 'https://s3-us-west-2.amazonaws.com/dynamodb-local/release'
    }
    flatDir {
        dirs 'libs'
    }
}

project.sourceCompatibility = JavaVersion.VERSION_14
project.targetCompatibility = JavaVersion.VERSION_14

group = 'uk.gov.justice.probation'

ext {
    swaggerVersion = '2.9.2'
    restAssuredVersion = '4.2.0'
}

def today = Instant.now()
def todaysDate = LocalDate.now().format('yyyy-MM-dd')
version = System.getenv('CI') ? "${todaysDate}.${System.getenv('CIRCLE_BUILD_NUM')}" : todaysDate

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    agentDeps.transitive = false
}

jar {
    manifest {
        attributes (
            'Implementation-Title': rootProject.name, 'Implementation-Version': archiveVersion
        )
    }
}

flyway {
    url = 'jdbc:postgresql://localhost:5432/postgres?currentSchema=courtcaseservice&user=root&password=dev'
}

springBoot {
    buildInfo {
        properties {
            artifact = rootProject.name
            version = version
            group = group
            name = rootProject.name
            time = today
            additional = [
                    by                   : System.properties['user.name'],
                    operatingSystem      : "${System.properties['os.name']} (${System.properties['os.version']})",
                    continuousIntegration: System.getenv('CI') ? true : false,
                    machine              : InetAddress.localHost.hostName
            ]
        }
    }
}

test {
    useJUnitPlatform ()
    filter {
        excludeTestsMatching '*PactTest'
        excludeTestsMatching '*SmokeTest'
        excludeTestsMatching '*WIP*'
    }

    testLogging {
        events "started", "passed", "skipped", "failed", "standardError"
        exceptionFormat "short"
        showStackTraces = true
        showExceptions = true
        showCauses = true

        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            }
        }
    }
}

task integrationTest(type: Test) {
    useJUnitPlatform ()
    filter {
        includeTestsMatching '*IntTest'
    }
}

task pactTestPublish(type: Test) {
    useJUnitPlatform ()
    filter {
        excludeTestsMatching '*IntTest'
        includeTestsMatching '*PactTest'
    }

    systemProperty("pact.provider.tag", System.getenv("PACT_PROVIDER_TAG"))
    systemProperty("pact.provider.version", System.getenv("PACT_PROVIDER_VERSION"))
    systemProperty("pact.verifier.publishResults", true)
    systemProperty("pactbroker.host",  System.getenv("PACTBROKER_HOST"))

    testLogging {
        events "started", "passed", "skipped", "failed", "standardError"
        exceptionFormat "short"
        showStackTraces = true
        showExceptions = true
        showCauses = true

        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            }
        }
    }
}

task unitTest(type: Test) {
    useJUnitPlatform ()
    filter {
        excludeTestsMatching '*IntTest'
        excludeTestsMatching '*SmokeTest'
        excludeTestsMatching '*WIP*'
    }
}

task wipTest(type: Test) {
    filter {
        includeTestsMatching '*WIP*'
    }
}

dependencies {
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    compileOnly 'org.projectlombok:lombok'

    compile 'org.springframework.boot:spring-boot-starter-webflux'
    compile 'org.projectreactor:reactor-spring:1.0.1.RELEASE'

    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation ('org.springframework.security:spring-security-oauth2-client') {
        exclude group: 'com.nimbusds', module: 'oauth2-oidc-sdk'
        because 'Known vulnerability and library not used'
    }
    runtimeOnly 'org.flywaydb:flyway-core'
    implementation "io.springfox:springfox-swagger2:$swaggerVersion"
    implementation "io.springfox:springfox-swagger-ui:$swaggerVersion"
    implementation 'io.jsonwebtoken:jjwt:0.9.1'
    implementation 'com.vladmihalcea:hibernate-types-52:2.10.4'

    runtimeOnly "org.springframework.boot:spring-boot-devtools"
    runtime 'org.postgresql:postgresql:42.2.19'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'net.javacrumbs.json-unit:json-unit-assertj:2.25.0'
    testImplementation ("io.rest-assured:json-path:$restAssuredVersion")
    testImplementation ("io.rest-assured:xml-path:$restAssuredVersion")
    testImplementation ("io.rest-assured:rest-assured:$restAssuredVersion"){
        exclude group: 'com.sun.xml.bind', module: 'jaxb-osgi'
    }

    testImplementation(platform("org.junit:junit-bom:5.7.1"))
    testImplementation("org.junit.jupiter:junit-jupiter")

    testRuntimeOnly("org.junit.platform:junit-platform-launcher") {
        because 'allows tests to run from IDEs that bundle older version of launcher'
    }

    testCompile ("com.github.tomakehurst:wiremock-jre8:2.27.1")
    testCompile("org.springframework.retry:spring-retry:1.3.1")
    testImplementation("au.com.dius.pact.provider:junit5spring:4.2.2")
    testImplementation("au.com.dius:pact-jvm-provider-junit5:4.0.10") {
        exclude group: 'junit', module: 'junit'
    }

    implementation "com.github.kittinunf.fuel:fuel:2.3.1"
    implementation 'org.springframework.security:spring-security-oauth2-resource-server'
    implementation 'org.springframework.security:spring-security-oauth2-jose'
    implementation 'com.microsoft.azure:applicationinsights-spring-boot-starter:2.6.3'
    implementation 'com.microsoft.azure:applicationinsights-logging-logback:2.6.3'
    implementation 'net.logstash.logback:logstash-logback-encoder:6.6'

    agentDeps 'com.microsoft.azure:applicationinsights-agent:3.0.3'

    testImplementation ('org.eclipse.jetty:jetty-io:9.4.39.v20210325') {
        because 'Transitive dependency. Previous versions have a known vulnerability'
    }
}

jacoco {
    toolVersion = '0.8.5'
}

jacocoTestReport {
    reports {
        html.enabled = false
        xml.enabled = true
        csv.enabled = false
        html.destination file("${buildDir}/jacocoHtml")
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            enabled = true
            element = 'CLASS'
            includes = ['uk.gov.justice.probation.*']
            excludes = ['uk.gov.justice.probation.courtcaseservice.jpa.repository.*']

            limit {
                counter = 'LINE'
                value = 'TOTALCOUNT'
                minimum = 0.8
            }
            limit {
                counter = 'METHOD'
                value = 'TOTALCOUNT'
                minimum = 0.8
            }
        }
    }
}

task copyAgent(type: Copy) {
    from configurations.agentDeps
    into "$buildDir/libs"
}

task copyAgentConfig(type: Copy) {
    from "applicationinsights.json"
    into "$buildDir/libs"
}

assemble.dependsOn copyAgent
assemble.dependsOn copyAgentConfig
